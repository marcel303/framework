static const char * s_hqCommonPs = R"SHADER(

include engine/ShaderUtil.txt

#if _SHADER_DEBUGGING_
	uniform float disableOptimizations;
	uniform float disableAA;
	uniform float _debugHq;
	
	#define enableOptimizations (disableOptimizations == 0.0)
	#define enableAA (disableAA == 0.0)
	#define debugHq (_debugHq != 0.0)
#else
	#define enableOptimizations true
	#define enableAA true
	#define debugHq false
#endif

shader_in vec2 v_p;
shader_in vec2 v_texcoord;

// color coordinate matrix
uniform mat4x4 gmat;

// texture coordinate matrix
uniform mat4x4 tmat;

uniform vec3 shadingParams; // gradientType, textureEnabled, useScreenSize
#define gradientType shadingParams.x
#define textureEnabled shadingParams.y
#define useScreenSize shadingParams.z

// color gradient
uniform mat4x4 gradientInfo;
#define gradientParams gradientInfo[0] // type, bias, scale, colorMode
#define gradientColor1 gradientInfo[1]
#define gradientColor2 gradientInfo[2]

// texture
uniform sampler2D source;

// global params
uniform vec4 params;

float computeDistance(vec2 p);
float distanceFade(float distance);

bool sampleDistanceFunctionHasBeenCalled = false;

float sampleDistanceFunction(vec2 p)
{
	float v = 0.0;

	sampleDistanceFunctionHasBeenCalled = true;
	
	if (enableAA)
	{
		for (int ox = -2; ox <= +2; ++ox)
		{
			for (int oy = -2; oy <= +2; ++oy)
			{
				vec2 offset = vec2(ox, oy) * 0.25;
				
				v += distanceFade(computeDistance(p + offset));
			}
		}
		
		v /= 25.0;
	}
	else
	{
		v = distanceFade(computeDistance(p));
	}
	
	return v;
}

vec4 hqGradientColor()
{
	mat4x4 gradientCoordMatrix = gmat;

	vec4 gradientCoord = gradientCoordMatrix * vec4(v_p, 0.0, 1.0);

	// use color gradient to determine color

	float t = 0.0;

	if (gradientType == 1.0)
	{
		// linear gradient

		t = gradientCoord.x;
	}
	else
	{
		// radial gradient

		t = length(gradientCoord.xy);
	}

	t = (t - gradientParams.y) * gradientParams.z;

	// color clamp

	if (params.w == 1.0)
	{
		t = clamp(t, 0.0, 1.0);
	}

	vec4 color = mix(gradientColor1, gradientColor2, t);

	return color;
}

vec4 hqTextureColor()
{
	mat4x4 textureCoordMatrix = tmat;

	vec4 textureCoord = textureCoordMatrix * vec4(v_p, 0.0, 1.0);

	vec4 color = texture(source, textureCoord.xy);

	return color;
}

// todo : make this code reusable in ShaderPS

vec4 hqApplyColorMode(vec4 color1, vec4 color2, float mode)
{
	/*
	COLOR_MUL,
	COLOR_ADD,
	COLOR_SUB,
	COLOR_IGNORE
	*/

	vec4 result = color1;
	vec4 texColor = color2;

	if (mode == 0.0)
	{
		result.rgb = result.rgb * texColor.rgb;
		result.a   = result.a   * texColor.a;
	}
	else if (mode == 1.0)
	{
		result.rgb = result.rgb + texColor.rgb;
		result.a   = result.a   * texColor.a;
	}
	else if (mode == 2.0)
	{
		result.rgb = result.rgb - texColor.rgb;
		result.a   = result.a   * texColor.a;
	}
	else if (mode == 3.0)
	{
		result.rgb = texColor.rgb;
		result.a   = texColor.a;
	}

	return result;
}

vec4 hqColor(float v, vec4 color)
{
	if (gradientType != 0.0)
	{
		vec4 gradientColor = hqGradientColor();

		color = hqApplyColorMode(color, gradientColor, gradientParams.w);
	}

	if (textureEnabled != 0.0)
	{
		vec4 textureColor = hqTextureColor();

		color = hqApplyColorMode(color, textureColor, params.y);
	}

	color.a *= v;

	color = applyColorPost(color, params.z);

	if (debugHq)
	{
		if (sampleDistanceFunctionHasBeenCalled == false)
		{
			color.rgb += vec3(0.0, 0.2, 0.5);
			color.a += 0.5;
		}
		else
		{
			color.rgb += vec3(0.5, 0.0, 0.0);
			color.a += 0.5;
		}

		color = clamp(color, vec4(0.0), vec4(1.0));
	}

	return color;
}

)SHADER";
