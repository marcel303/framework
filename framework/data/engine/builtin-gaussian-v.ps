include engine/ShaderPS.txt

shader_in vec2 texcoord;
uniform sampler2D source;
uniform float kernelSize;
uniform float radius;

uniform vec4 weights[32] buffer filterKernel; // note : OpenGL's layout(std140) aligns each float in float[] to 16 bytes.. which is pretty useless. so we use vec4[] instead

float getWeight(int i)
{
	vec4 v = weights[i >> 2];

	i &= 3;

	return v[i];
}

void main()
{
    float texScale = radius / float(textureSize(source, 0).y) / (kernelSize - 1.0);
	
	vec4 result = texture(source, texcoord) * getWeight(0);
	
	for (int i = 1; i < int(kernelSize); ++i)
	{
		result += texture(source, texcoord + vec2(0.0, texScale * float(i))) * getWeight(i);
		result += texture(source, texcoord - vec2(0.0, texScale * float(i))) * getWeight(i);
	}
	
    shader_fragColor = result;
}
