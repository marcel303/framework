include engine/ShaderPS.txt
include engine/ShaderUtil.txt

#define kToneMap_Uncharted2 0
#define kToneMap_JimRichard 1
#define kToneMap_Reinhard   2
#define kToneMap_Aces       3

uniform sampler2D colorTexture;

uniform float tonemap;
uniform float exposure;
uniform float gamma;

shader_in vec2 texcoord;

// Uncharted 2 tone map
// by: John Hable
// aka 'Hable Tone Mapping', 'Hable Filmic'
// source: https://www.shadertoy.com/view/lslGzl
vec3 Uncharted2ToneMapping(vec3 color)
{
	float A = 0.15;
	float B = 0.50;
	float C = 0.10;
	float D = 0.20;
	float E = 0.02;
	float F = 0.30;
	
	float white = 11.2;

	color = ((color * (A * color + C * B) + D * E) / (color * (A * color + B) + D * F)) - E / F;
	white = ((white * (A * white + C * B) + D * E) / (white * (A * white + B) + D * F)) - E / F;
	color *= (1.0 / white);

	return color;
}

// optimized formula by Jim Hejl and Richard Burgess-Dawson
// source: http://filmicworlds.com/blog/filmic-tonemapping-operators/
// note : it's modeled to have to linear to sRGB conversion built-in
//        sadly, we undo it here, so it behaves the same as the others
vec3 toneMapJimRich(vec3 color)
{
    color = max(vec3(0.0), color - vec3(0.004));
    color = (color * (6.2 * color + vec3(0.5))) / (color * (6.2 * color + vec3(1.7)) + vec3(0.06));

    color = srgbToLinear(color);

    return color;
}

// Reinhard tone map
// source: http://filmicworlds.com/blog/filmic-tonemapping-operators/
vec3 toneMapReinhard(vec3 color)
{
   color = color / (vec3(1.0) + color);
   
   return color;
}

// ACES (Academy Color Encoding System) tone map
// source: https://knarkowicz.wordpress.com/2016/01/06/aces-filmic-tone-mapping-curve/
vec3 toneMapACES(vec3 color)
{
	float A = 2.51;
    float B = 0.03;
    float C = 2.43;
    float D = 0.59;
    float E = 0.14;

    color = clamp((color * (A * color + B)) / (color * (C * color + D) + E), 0.0, 1.0);

    return color;
}

/*
Curve was manually fitted (max fit error: 0.0138) to be more precise in
the blacks – after all we will be applying some kind gamma afterwards.
Additionally, data was pre-exposed, so 1 on input maps to ~0.8 on output
and resulting image’s brightness is more consistent with the one without
any tone mapping curve at all. For the original ACES curve just multiply
input (x) by 0.6.
*/
vec3 toneMapACES_pure(vec3 color)
{
	return toneMapACES(color * 0.6);
}

void main()
{
	vec3 color = texture(colorTexture, texcoord).rgb;

	color = max(vec3(0.0), color);

	color *= exposure;

#if 1
	int i_tonemap = int(tonemap);

	if (i_tonemap == kToneMap_Uncharted2)
		color = Uncharted2ToneMapping(color);
	else if (i_tonemap == kToneMap_JimRichard)
		color = toneMapJimRich(color);
	else if (i_tonemap == kToneMap_Reinhard)
		color = toneMapReinhard(color);
	else if (i_tonemap == kToneMap_Aces)
		color = toneMapACES_pure(color);
#else
	//color = Uncharted2ToneMapping(color);
	color = toneMapJimRich(color);
	//color = toneMapReinhard(color);
	//color = toneMapACES(color);
	//color = toneMapACES_pure(color);
#endif

	//color = pow(color, vec3(1.0 / gamma));
	color = linearToSrgb(color);

	color += colorDither8ScreenSpace(texcoord * vec2(textureSizef(colorTexture, 0))) * 4.0;

	shader_fragColor = vec4(color, 1.0);
}