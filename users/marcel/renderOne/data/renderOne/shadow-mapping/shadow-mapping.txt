uniform sampler2D shadowDepthAtlas;
uniform sampler2D shadowColorAtlas;

uniform mat4x4 viewToShadowMatrices[32];
uniform mat4x4 shadowToViewMatrices[32];

uniform float numShadowMaps;
uniform float enableColorShadows;

#define ENABLE_PCF 0

#define ENABLE_SHADOW_DEBUGS 0

vec3 lookupShadow(int id, vec3 position_view)
{
	// transform view-space position into light-space

	vec4 position_light = viewToShadowMatrices[id] * vec4(position_view, 1.0);

#if !defined(GL_core_profile)
	position_light.y = -position_light.y;
#endif

	position_light.xyz /= position_light.w;

	float position_depth = position_light.z;

#if defined(GL_core_profile)
	position_depth = (position_depth + 1.0) * 0.5; // pack depth from -1.0 to +1.0 range to the 0.0 to 1.0 range used when storing depth
#endif

	// does the point lie before the near plane of the light?

	if (position_depth < 0.0)
	{
		// if the point lies before the near plane of the light,
		// we don't know if there's geometry drawn between it and
		// the near plane of the light. we just assume it isn't
		// shadowed to avoid too glaring discontinuities with
		// things that are past the near plane. note that for
		// spot lights, things before the near plane will not be
		// lit anyway (due to the spot light's attenuation). for
		// directional lights the above generally works best

		return vec3(1.0);
	}

	// determine texture coordinates

	vec2 position_texture = position_light.xy;

	position_texture = (position_texture + vec2(1.0)) * 0.5;

	// perform texture sampling inside the atlas texture

// todo : optimize : store shadow atlas min/max inside a uniform array
	vec2 atlas_min = vec2(1.0 / numShadowMaps * (id + 0), 0.0);
	vec2 atlas_max = vec2(1.0 / numShadowMaps * (id + 1), 1.0);

	vec2 position_atlas = mix(atlas_min, atlas_max, position_texture);

	position_atlas = max(atlas_min, min(atlas_max, position_atlas));

	float depth = texture(shadowDepthAtlas, position_atlas).x;

#if ENABLE_SHADOW_DEBUGS

	//if (position_light.z < 0.0) return vec3(1, 0, 1);
	//if (depth < 0.0) return vec3(0, 0, 1);
	//if (depth > 1000.0) return vec3(1, 0, 0);

#if 0
	if (position_atlas.x < atlas_min.x || position_atlas.x > atlas_max.x)
		return vec3(1, 0, 0);
	if (position_atlas.y < atlas_min.y || position_atlas.y > atlas_max.y)
		return vec3(0, 0, 1);
#endif

#if 1
	if (position_texture.x < 0.0 || position_texture.x > 1.0)
		return vec3(1, 0, 0);
	if (position_texture.y < 0.0 || position_texture.y > 1.0)
		return vec3(0, 0, 1);
#endif

	//return vec3(atlas_min, 0);
	//return vec3(atlas_max, 0);
	//return vec3(position_atlas, 0.0);
	//return vec3(depth);
	//return v_position;

	//return vec3(position_texture, 0.0);
	//return vec3(0.5);
	//return position_light.zzz;
	//return vec3(abs(position_light.z - depth));

#endif

#if ENABLE_PCF
	// 'percentage closer filtering'
	float depth00 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(-1, -1)).x;
	float depth10 = textureOffset(shadowDepthAtlas, position_atlas, ivec2( 0, -1)).x;
	float depth20 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(+1, -1)).x;
	float depth01 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(-1,  0)).x;
	float depth11 = textureOffset(shadowDepthAtlas, position_atlas, ivec2( 0,  0)).x;
	float depth21 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(+1,  0)).x;
	float depth02 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(-1, +1)).x;
	float depth12 = textureOffset(shadowDepthAtlas, position_atlas, ivec2( 0, +1)).x;
	float depth22 = textureOffset(shadowDepthAtlas, position_atlas, ivec2(+1, +1)).x;

	float occlusionMask =
		(depth00 < position_depth ? 0.0 : 1.0) +
		(depth10 < position_depth ? 0.0 : 1.0) +
		(depth20 < position_depth ? 0.0 : 1.0) +
		(depth01 < position_depth ? 0.0 : 1.0) +
		(depth11 < position_depth ? 0.0 : 1.0) +
		(depth21 < position_depth ? 0.0 : 1.0) +
		(depth02 < position_depth ? 0.0 : 1.0) +
		(depth12 < position_depth ? 0.0 : 1.0) +
		(depth22 < position_depth ? 0.0 : 1.0);

	occlusionMask *= (1.0 / 9.0);
#else
	float occlusionMask = 1.0;

	if (depth < position_depth)
	{
		occlusionMask = 0.0;
	}
#endif

	if (enableColorShadows != 0.0 && occlusionMask != 0.0)
	{
	#if ENABLE_PCF
		return occlusionMask * texture(shadowColorAtlas, position_atlas).rgb;
	#else
		return texture(shadowColorAtlas, position_atlas).rgb;
	#endif
	}
	else
	{
		return vec3(occlusionMask);
	}
}

vec3 lookupShadowPosition(int id, vec3 position_view)
{
	// transform view-space position into light-space

	vec4 position_light = viewToShadowMatrices[id] * vec4(position_view, 1.0);

	// does the point lie before the near plane of the light?

	if (position_light.w <= 0.0)
	{
		return position_view;
	}

	position_light.xy /= position_light.w;

	// does the point lie outside of the texture?

	if (position_light.x < -1.0 || position_light.x > +1.0)
		return position_view;
	if (position_light.y < -1.0 || position_light.y > +1.0)
		return position_view;

	// determine texture coordinates

	vec2 position_texture = position_light.xy;

#if !defined(GL_core_profile)
	position_texture.y = 1.0 - position_texture.y;
#endif

	position_texture = (position_texture + vec2(1.0)) * 0.5;

	// perform texture sampling inside the atlas texture

// todo : optimize : store shadow atlas min/max inside a uniform array
	vec2 atlas_min = vec2(1.0 / numShadowMaps * (id + 0), 0.0);
	vec2 atlas_max = vec2(1.0 / numShadowMaps * (id + 1), 1.0);

	vec2 position_atlas = mix(atlas_min, atlas_max, position_texture);

	float depth = texture(shadowDepthAtlas, position_atlas).x;

#if defined(GL_core_profile)
	depth = depth * 2.0 - 1.0;
#endif

	//depth = position_light.z; // when set to this, the result of this function should equal position_view passed into it

	vec3 shadowPosition_clip = vec3(position_light.xy, depth);
	vec4 shadowPosition_proj = vec4(shadowPosition_clip, 1.0);
	vec4 shadowPosition_view = shadowToViewMatrices[id] * shadowPosition_proj;

	shadowPosition_view.xyz /= shadowPosition_view.w;

	return shadowPosition_view.xyz;
}

#undef ENABLE_SHADOW_DEBUGS
