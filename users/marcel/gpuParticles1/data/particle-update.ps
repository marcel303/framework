include engine/ShaderPS.txt

// todo : merge position and velocity textures
uniform sampler2D p;

uniform sampler2D flowfield;

uniform float drag;
uniform vec2 grav_pos;

uniform float grav_force;
uniform float flow_strength;

uniform vec4 bounds;
uniform vec4 boundsParams;

int boundsModeX = int(boundsParams.x);
int boundsModeY = int(boundsParams.y);
bool boundsEnabled = boundsParams.z != 0.0;

shader_in vec2 texcoord;

void main()
{
	vec4 positionAndVelocity = texture(p, texcoord);
	vec2 position = positionAndVelocity.xy;
	vec2 velocity = positionAndVelocity.zw;

	// apply gravitational force

	vec2 delta = grav_pos - position;
	velocity += normalize(delta) / length(delta) * grav_force;

	// apply flowfield acceleration

// todo : include time step
	vec2 flow = texture(flowfield, position / textureSize(flowfield, 0)).xy;
	velocity += flow * flow_strength;

	velocity *= drag;

	if (boundsEnabled)
	{
		// apply bounds constaint

		vec2 min = bounds.xy;
		vec2 max = bounds.zw;

		if (boundsModeX == 1)
		{
			if (position.x < min.x)
				velocity.x = +abs(velocity.x);
			if (position.x > max.x)
				velocity.x = -abs(velocity.x);
		}
		else if (boundsModeX == 2)
		{
			if (position.x < min.x)
				position.x += max.x - min.x;
			if (position.x > max.x)
				position.x -= max.x - min.x;
		}

		if (boundsModeY == 1)
		{
			if (position.y < min.y)
				velocity.y = +abs(velocity.y);
			if (position.y > max.y)
				velocity.y = -abs(velocity.y);
		}
		else if (boundsModeY == 2)
		{
			if (position.y < min.y)
				position.y += max.x - min.y;
			if (position.y > max.y)
				position.y -= max.y - min.y;
		}
	}

	position += velocity;
	
	shader_fragColor = vec4(position, velocity);
}